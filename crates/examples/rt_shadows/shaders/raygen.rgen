#version 460
#extension GL_EXT_ray_tracing : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 1, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform SceneData {
	mat4 invertedView;
	mat4 invertedProj;
	vec3 lightDirection;
	vec3 lightColor;
} scene;

layout(location = 0) rayPayloadEXT vec3 hitValue;

void main() {
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
	const vec2 d = (inUV * 2.0 - 1.0);

	vec4 origin = scene.invertedView * vec4(0, 0, 0, 1);
	vec4 target = scene.invertedProj * vec4(d.x, d.y, 1, 1) ;
	vec4 direction = scene.invertedView * vec4(normalize(target.xyz), 0) ;

	float tmin = 0.001;
	float tmax = 100.0;

    hitValue = vec3(0.0);

	const uint missIndex = 0;

    traceRayEXT(
		topLevelAS, 
		gl_RayFlagsOpaqueEXT, 
		0xff, 
		0, 0, 
		missIndex, 
		origin.xyz, 
		tmin, 
		direction.xyz, 
		tmax, 
		0
	);

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 0.0));
}
