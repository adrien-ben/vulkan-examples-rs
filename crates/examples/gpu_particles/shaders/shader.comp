#version 450

struct Particle {
  vec4 position;
  vec4 direction;
  vec3 color;
};

layout(std430, binding = 0) buffer Buffer {
   Particle particles[];
};

layout(binding = 1) uniform Config {
  vec3 attractorCenter;
  float attractorStrength;
  uint particleCount;
  float elasped;
} cfg;

layout (local_size_x = 256) in;

float rand(vec2 co){
    return fract(sin(dot(co, vec2(12.9898, 78.233))) * 43758.5453);
}

void main() {
    // Current SSBO index
    uint index = gl_GlobalInvocationID.x;

    // Don't try to write beyond particle count
    if (index >= cfg.particleCount) {
      return;
    }

    // Read particle attribute
    vec3 position = particles[index].position.xyz;
    vec3 direction = normalize(particles[index].direction.xyz);

    const vec3 MAX_SPEED = vec3(100.0);

    // compute velocity
    float distanceToMass = length(cfg.attractorCenter - position);
    float speedFactor = rand(gl_GlobalInvocationID.xx) * 0.2 + 0.8;
    vec3 toMass = normalize(cfg.attractorCenter - position) * distanceToMass * cfg.elasped * cfg.attractorStrength * speedFactor;
    direction = clamp(direction + toMass, -MAX_SPEED, MAX_SPEED);

    // Move by velocity
    position += direction * cfg.elasped;

    // Write back
    particles[index].position.xyz = position;
    particles[index].direction.xyz = direction;
}